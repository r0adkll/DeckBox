apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'io.fabric'

def GIT_SHA = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def GIT_TAG =  'git describe --tags'.execute([], project.rootDir).text.trim()

def VERSION_NAME = "1.8.1"


ext.gitVersioner = [
        defaultBranch           : "master",
        stableBranches          : ["release"],
        yearFactor              : 1200,
        snapshotEnabled         : false,
        localChangesCountEnabled: false,
        shortName: { gitVersion ->
            return gitVersion.commit.subSequence(0, 7)
        }
]
apply from: 'https://raw.githubusercontent.com/passsy/gradle-GitVersioner/master/git-versioner.gradle'


android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.r0adkll.deckbuilder"
        buildToolsVersion "28.0.3"

        minSdkVersion 21
        targetSdkVersion 28

        versionCode gitVersion.version
        versionName "$VERSION_NAME.${gitVersion.version}"
        multiDexEnabled true

        buildConfigField "String", "GIT_SHA", "\"$GIT_SHA\""
        buildConfigField "String", "GIT_TAG", "\"$GIT_TAG\""
        buildConfigField "String", "DATABASE_NAME", "\"deckbox.db\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                outputFileName = "deckbox-" + variant.versionName + "." + GIT_SHA.toUpperCase() + ".apk"
            }
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    androidTestImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    androidTestImplementation "androidx.test:runner:$runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.pokemontcg:pokemon-tcg-sdk-kotlin:$poketcg_version"

    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:$support_version"
    implementation "androidx.cardview:cardview:$support_version"
    implementation "androidx.preference:preference:$support_version"
    implementation "androidx.legacy:legacy-preference-v14:$support_version"
    implementation "com.google.android.material:material:$support_version"
    implementation "androidx.palette:palette:$support_version"

    implementation "com.google.firebase:firebase-core:16.0.6"
    implementation "com.google.firebase:firebase-firestore:18.0.0"
    implementation "com.google.firebase:firebase-auth:16.1.0"
    implementation "com.google.firebase:firebase-config:16.1.3"
    implementation "com.google.android.gms:play-services-auth:16.0.1"
    implementation "com.google.android.gms:play-services-oss-licenses:16.0.2"
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'com.caverock:androidsvg-aar:1.3'

    implementation "com.evernote:android-state:$state_version"
    kapt "com.evernote:android-state-processor:$state_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'nz.bradcampbell:paperparcel:2.0.6'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.6'
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.6'

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rxbinding_version"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rxrelay_version"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:$rxpref_version"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation("com.52inc:52Kit-core:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-winds:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-kotlin:$ftkit_version") {
        exclude group: 'io.reactivex'
    }
    implementation("com.52inc:52Kit-arch:$ftkit_version") {
        exclude group: 'io.reactivex'
    }

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.roughike:bottom-bar:$bottombar_version"
    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.github.chrisbanes:PhotoView:2.2.0'
    debugImplementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

}

apply plugin: 'com.google.gms.google-services'
apply from: '../tools/pokken.gradle'
