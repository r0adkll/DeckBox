apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.oss.licenses.plugin'
apply plugin: 'io.fabric'

def VERSION_NAME = "1.10.0"

gitVersioner {
    baseBranch 'master'
    yearFactor 1200
    addSnapshot false
    addLocalChangesDetails false
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.r0adkll.deckbuilder"
        buildToolsVersion "29.0.2"

        minSdkVersion 22
        targetSdkVersion 29

        versionCode gitVersioner.versionCode
        versionName "$VERSION_NAME.${gitVersioner.versionName}"
        multiDexEnabled true

        buildConfigField "String", "GIT_SHA", "\"${gitVersioner.currentSha1Short}\""
        buildConfigField "String", "DATABASE_NAME", "\"deckbox.db\""
        buildConfigField "String", "AFFILIATE_CODE", "\"DeckBox\""
        buildConfigField "String", "PREVIEW_API_URL", "\"https://deckboxtcg.app/api/v1/\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        xmlReport true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests {
        includeAndroidResources = true

        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                outputFileName = "deckbox-" + variant.versionName + "." + gitVersioner.currentSha1Short + ".apk"
            }
        }
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

androidExtensions {
    experimental = true
}

ext {
    junit_version = '4.12'
    espresso_version = '3.1.0-alpha4'
    mockito_version = '1.10.19'
    runner_version = '1.1.0-alpha4'
    kluent_version = '1.42'

    // Dependencies
    androidXCoreVersion = '1.2.0-beta01'
    bottombar_version = '2.3.1'
    butterknife_version = '8.8.1'
    canvas_version = '1.0.2'
    ftkit_version = '2.0.6'
    leakcanary_version = '1.5.1'
    play_version = '15.0.0'
    poketcg_version = '1.0.19'
    retrofit_version = '2.3.0'
    room_version = '2.2.1'
    support_version = '1.0.0'
    state_version = '1.4.1'
    stetho_version = '1.5.1'
    timber_version = '4.7.0'
    okhttp_version = '3.8.1'
}

dependencies {
    testImplementation "junit:junit:$junit_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    androidTestImplementation "org.amshove.kluent:kluent-android:$kluent_version"
    androidTestImplementation "androidx.test:runner:$runner_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:1.5.4"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:1.5.4"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "io.pokemontcg:pokemon-tcg-sdk-kotlin:$poketcg_version"

    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0-rc01"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.preference:preference:1.1.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "com.google.android.material:material:1.2.0-alpha01"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.browser:browser:1.2.0-alpha09"

    implementation "dev.chrisbanes:insetter:0.1.1"
    implementation "dev.chrisbanes:insetter-ktx:0.1.1"

    implementation "com.google.firebase:firebase-core:17.2.1"
    implementation "com.google.firebase:firebase-firestore:21.2.1"
    implementation "com.google.firebase:firebase-auth:19.1.0"
    implementation "com.google.firebase:firebase-config:19.0.3"
    implementation "com.google.android.gms:play-services-auth:17.0.0"
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    releaseImplementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    implementation "com.google.dagger:dagger:2.23.1"
    kapt "com.google.dagger:dagger-compiler:2.23.1"

    implementation "com.github.bumptech.glide:glide:4.9.0"
    kapt "com.github.bumptech.glide:compiler:4.9.0"
    implementation 'com.caverock:androidsvg-aar:1.3'

    implementation "com.evernote:android-state:$state_version"
    kapt "com.evernote:android-state-processor:$state_version"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    implementation 'nz.bradcampbell:paperparcel:2.0.8'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.8'
    implementation 'nz.bradcampbell:paperparcel-api:2.0.8'
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.8'

    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.2.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.2.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:2.2.0"
    implementation "com.jakewharton.rxrelay2:rxrelay:2.1.0"
    implementation "com.f2prateek.rx.preferences2:rx-preferences:2.0.0"

    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    implementation "com.ftinc:kit:$ftkit_version"
    implementation "com.ftinc:kit-arch:$ftkit_version"

    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.roughike:bottom-bar:$bottombar_version"
    implementation 'com.nex3z:flow-layout:1.2.2'
    implementation 'com.sothree.slidinguppanel:library:3.4.0'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    debugImplementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.robinhood.spark:spark:1.2.0'

}

apply plugin: 'com.google.gms.google-services'
apply from: '../tools/pokken.gradle'
